!  HydroAQWA2FAST.f90 
!
!  FUNCTIONS:
!  HydroAQWA2FAST - Entry point of console application.
!

!****************************************************************************
!
!  PROGRAM: HydroAQWA2FAST
!
!  PURPOSE:  This code is to convert hydrodynamic coefficients 
!            generated by AQWA to the WAMIT format for the use in FAST.
!  Developed by: Yang Yang. University of Shanghai for Science and Technology
!       Date : 12-Mar-2020
!
!****************************************************************************

    program HydroAQWA2FAST

    implicit none

    character(len=1204) ::  AQWAInFile		!- File name of the AQWA output (.AH1) 
    character(len=1204) ::  OutBsName		!- The base name of the output files (OutBsName.hst, OutBsName.1 and OutBsName.3 will be output)
    integer,parameter :: UnAQ = 11          !- I/O for the AQWA input
    integer,parameter :: UnFST0 = 12        !- I/O for the hydrostatic stiffness file (.hst)
    integer,parameter :: UnFST1 = 13        !- I/O for the added mass and damping file (.1)
    integer,parameter :: UnFST3 = 14        !- I/O for the excitation force file (.3)
    ! Variables
    real,parameter :: PI=3.1415926
    real,parameter :: R2D=180.0/PI  
    
    ! Variables from the input
    real :: Rho                         ! Density of water
    real :: G                           ! Gravitational  acceleration
    integer :: NumHds                   ! Number of headings
    integer :: NumFreqs                 ! Number of frequencies
    real :: DispVol                     ! Displaced volume 
    
    ! Local variables 
    integer :: NumStr                   ! Number of structures
    character(len=1024) :: TempLine     !  to store the first 6 headings
    real, allocatable :: Hds(:)         ! Headings
    real, allocatable :: Freqs(:)       ! Frequencies
    real :: Dpth
    real :: Drft
    real :: COG(3),COB(3)
    real :: MASS (6,6)
    real :: HydroStif (6,6)
    real,allocatable :: AddedMass (:,:,:)
    real,allocatable :: Damping (:,:,:)
    real :: AddedMassLF(6,6)
    real :: AddedMassHF(6,6)
    real :: DampingLF(6,6)
    real :: DampingHF(6,6)
    real,allocatable :: Force(:,:,:)
    real,allocatable :: Phase(:,:,:)
    
    integer :: tempItg1,tempItg2,tempItg3,i,j,k
    integer :: NumHdsLines,NumFreqsLines
    
    ! Body of HydroAQWA2FAST
    ! Read the input the inputfile
    open (1,file = 'InputforHydroAQWA2FAST.txt')
     read(1,*)
     read(1,*)AQWAInFile
     read(1,*)OutBsName
    close(1)
! Write the declaration
    write (*,*) '---------------------------------------------------------------------------',&
                ' This code is developed by Dr Y.Yang from University of Shanghai for Science ',&
                ' and Technology on 12-Mar-2020 for converting hydrodynamic coefficients of ',&
                ' a floating platform to the WAMIT format using the .AH1 file generated by ',&
                ' AQWA for use in FAST.                                                 ',&
                '---------------------------------------------------------------------------'

    ! Open the files ready to read and write
    open (UnAQ, file = trim(AQWAInFile))
     write (*,*)
     write (*,*) ' Reading data from the file: '//trim(AQWAInFile) 
    ! read the headings and frequencies
    do i=1,5 ! ship the comments
       read(UnAQ,*)
    enddo
     read (UnAQ,'(A1024)') TempLine
     read (TempLine(1:13),*) NumStr,NumHds,NumFreqs
     write(*,'(I4,A,I3,A)') NumHds,' wave headings and ',NumFreqs, ' frequencies included'
    allocate (Hds(NumHds))
    allocate (Freqs(NumFreqs))
    TempLine = TempLine(13:1024)
    if (NumHds<=6) then
       read (TempLine,*) (Hds(i),i=1,NumHds)
    else
       NumHdsLines = floor(NumHds/6.0)
       if (NumHdsLines<3) then
          read (TempLine,*) (Hds(i),i=1,6)
          read (UnAQ,*) (Hds(i),i=7,NumHds)
       else
          read (TempLine,*) (Hds(i),i=1,6)
          do i=1,NumHdsLines-1
             read (UnAQ,*) (Hds(j),j=i*6+1,i*6+6)
          enddo
          if ((NumHds - 6*NumHdsLines) > 0) then
             read (UnAQ,*) (Hds(j),j=NumHdsLines*6+1,NumHds)
          endif
       endif
    endif
    
    if (NumFreqs < 7 ) then
       read (UnAQ,*) (Freqs(j),j=1,NumFreqs)
    else
       NumFreqsLines = floor(NumFreqs/6.0)
       do i=1, NumFreqsLines
          read (UnAQ,*) (Freqs(j),j=(i-1)*6+1,i*6)
       enddo
       if ((NumFreqs - 6*NumFreqsLines) > 0) then
             read (UnAQ,*) (Freqs(j),j=NumFreqsLines*6+1,NumFreqs)
       endif
    endif
    !write(*,*)  "Periods =",2*PI/Freqs
    read (UnAQ,*)  ! General
    read (UnAQ,*)  NumStr,Dpth,Rho,G
    read (UnAQ,*)  ! COG
    read (UnAQ,*)  NumStr,COG(1),COG(2),COG(3)
     !write (*,*) 
     !write (*,'(A,3(F8.3),A)') " CoG: ",COG(1),COG(2),COG(3),' (m)'
    read (UnAQ,*)  ! Draft
    read (UnAQ,*)  NumStr,Drft
    read (UnAQ,*)  ! CoB
    read (UnAQ,*)  NumStr,COB(1),COB(2),COB(3)
     !write (*,'(A,3(F8.3),A)') " CoB: ",COB(1),COB(2),COB(3),' (m)'
    read (UnAQ,*)  ! DISPLACEMENT
     !write (*,"(A,F6.2,A)") " Water depth: ", Dpth,' (m)'
     !write (*,"(A,F6.2,A)") " Draft: ", Drft,' (m)'
    read (UnAQ,*)  NumStr,DispVol
     !write (*,'(A,F9.2,A)') " Displaced volume: ",DispVol,' (m^3)'
    read (UnAQ,*)  ! MASS
    read (UnAQ,*) NumStr,(MASS(1,j),j=1,6)
    do i=2,6
       read (UnAQ,*) (MASS(i,j),j=1,6)
    enddo
    read (UnAQ,*)  ! HYDSTIFFNESS
    read (UnAQ,*) NumStr,(HydroStif(1,j),j=1,6)
    do i=2,6
       read (UnAQ,*) (HydroStif(i,j),j=1,6)
    enddo
! Added mass
    read (UnAQ,*)  ! ADDEDMASS
    allocate (AddedMass(NumFreqs,6,6))
    do i=1,NumFreqs
       read (UnAQ,*) tempItg1,tempItg2,tempItg3, (AddedMass(i,1,k),k=1,6)
       do j=2,6
          read (UnAQ,*) (AddedMass(i,j,k),k=1,6)
       enddo
    enddo
! Damping
    read (UnAQ,*)  ! DAMPING
    allocate (Damping(NumFreqs,6,6))
    do i=1,NumFreqs
       read (UnAQ,*) tempItg1,tempItg2,tempItg3, (Damping(i,1,k),k=1,6)
       do j=2,6
          read (UnAQ,*) (Damping(i,j,k),k=1,6)
       enddo
    enddo
! ADDEDMASS_LF
    read (UnAQ,*)  ! ADDEDMASS_LF
    read (UnAQ,*) tempItg1,tempItg2, (AddedMassLF(1,j),j=1,6)
    do i=2,6
       read (UnAQ,*) (AddedMassLF(i,j),j=1,6)
    enddo
! DAMPING_LF
    read (UnAQ,*)  ! DAMPING_LF
    read (UnAQ,*) tempItg1,tempItg2, (DampingLF(1,j),j=1,6)
    do i=2,6
       read (UnAQ,*) (DampingLF(i,j),j=1,6)
    enddo
! ADDEDMASS_HF
    read (UnAQ,*)  ! ADDEDMASS_HF
    read (UnAQ,*) tempItg1,tempItg2, (AddedMassHF(1,j),j=1,6)
    do i=2,6
       read (UnAQ,*) (AddedMassHF(i,j),j=1,6)
    enddo
! Get to the force rao line
    do
      read (UnAQ,'(A8)') TempLine
      if (trim(TempLine) == 'FORCERAO') exit
    enddo
! Loop to read force components
    allocate  (Force(NumHds,NumFreqs,6))
    allocate  (Phase(NumHds,NumFreqs,6))
    do i=1,NumHds
       do j=1,NumFreqs
          read (UnAQ,*) tempItg1,tempItg2,tempItg3, (Force(i,j,k),k=1,6)
          read (UnAQ,*) (Phase(i,j,k),k=1,6)
       enddo
    enddo
!-- AQWA phase angle is invese to that in WAMIT
    
!    Phase = -1.0* Phase
! Done reading, start the writing
    open (UnFST0, file = trim(OutBsName)//'.hst')
     !write (*,*) 
     !write (*,*) 'Wrting the non-dimensional hydrostiffness file: '//trim(OutBsName)//'.hst'
     do i=1,6
        do j=1,6
           write(UnFST0,'(2(I6),ES15.6)') i,j,HydroStif(i,j)/(Rho*G)
        enddo
     enddo
    close (UnFST0)    
    open (UnFST1, file = trim(OutBsName)//'.1')
     !write (*,*)
     !write (*,*) 'Wrting the non-dimensional added mass and damping file: '//trim(OutBsName)//'.1'
! 
     ! Write the added mass and damping file
     ! write the LF (-1s) and HF (0s) added mass
     do i=1,6
        do j=1,6
           write (UnFST1,'(ES15.6,2(I6),ES15.6)') -1.0,i,j,AddedMassLF(i,j)/Rho
        enddo
     enddo
     do i=1,6
        do j=1,6
           write (UnFST1,'(ES15.6,2(I6),ES15.6)') 0,i,j,AddedMassHF(i,j)/Rho
        enddo
     enddo
     do i=1,NumFreqs
        do j=1,6
           do k=1,6
              write (UnFST1,'(ES15.6,2(I6),2(ES15.6))') 2.0*PI/Freqs(i),j,k,AddedMass(i,j,k)/Rho,Damping(i,j,k)/(Rho*Freqs(i))
           enddo
        enddo
     enddo
    close (UnFST1)    
     
! Writing the force file
    Force = Force /(Rho*G)
    open (UnFST3, file = trim(OutBsName)//'.3')
     !write (*,*)
     !write (*,*) 'Wrting the non-dimensional excitation force file: '//trim(OutBsName)//'.3'
     do i=1,NumFreqs
        do j=1,NumHds
           do k=1,6
              write(UnFST3,'(2(ES15.6),I6,4(ES15.6))') 2.0*PI/Freqs(i),Hds(j),k,Force(j,i,k),Phase(j,i,k),cos(Phase(j,i,k)/R2D)*Force(j,i,k),sin(Phase(j,i,k)/R2D)*Force(j,i,k)
           enddo
        enddo
     enddo
    close (UnFST3)    
   
    ! All done
    write (*,*) 
    write (*,'(A)') '  All Done!',&
                    '  Please check the files: ',&
                    '  1: '//trim(OutBsName)//'.hst',&
                    '  2: '//trim(OutBsName)//'.1',&
                    '  3: '//trim(OutBsName)//'.3'
                                                          
    end program HydroAQWA2FAST

